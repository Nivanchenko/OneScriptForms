Перем Ф, КолОшибок, СписокОшибок, КолТестов;

Функция СтрНайтиМежду(СтрПараметр, Фрагмент1 = Неопределено, Фрагмент2 = Неопределено, ИсключитьФрагменты = Истина, БезНаложения = Истина)
	//Стр - исходная строка
	//Фрагмент1 - подстрока поиска от которой ведем поиск
	//Фрагмент2 - подстрока поиска до которой ведем поиск
	//ИсключитьФрагменты - не включать Фрагмент1 и Фрагмент2 в результат
	//БезНаложения - в результат не будут включены участки, содержащие дугие найденные участки, удовлетворяющие переданным параметрам
	//функция возвращает массив строк
	Стр = СтрПараметр;
	М = Новый Массив;
	Если (Фрагмент1 <> Неопределено) и (Фрагмент2 = Неопределено) Тогда
		Позиция = Найти(Стр, Фрагмент1);
		Пока Позиция > 0 Цикл
			М.Добавить(?(ИсключитьФрагменты, Сред(Стр, Позиция + СтрДлина(Фрагмент1)), Сред(Стр, Позиция)));
			Стр = Сред(Стр, Позиция + 1);
			Позиция = Найти(Стр, Фрагмент1);
		КонецЦикла;
	ИначеЕсли (Фрагмент1 = Неопределено) и (Фрагмент2 <> Неопределено) Тогда
		Позиция = Найти(Стр, Фрагмент2);
		СуммаПозиций = Позиция;
		Пока Позиция > 0 Цикл
			М.Добавить(?(ИсключитьФрагменты, Сред(Стр, 1, СуммаПозиций - 1), Сред(Стр, 1, СуммаПозиций - 1 + СтрДлина(Фрагмент2))));
			Позиция = Найти(Сред(Стр, СуммаПозиций + 1), Фрагмент2);
			СуммаПозиций = СуммаПозиций + Позиция;
		КонецЦикла;
	ИначеЕсли (Фрагмент1 <> Неопределено) и (Фрагмент2 <> Неопределено) Тогда
		Позиция = Найти(Стр, Фрагмент1);
		Пока Позиция > 0 Цикл
			Стр2 = ?(ИсключитьФрагменты, Сред(Стр, Позиция + СтрДлина(Фрагмент1)), Сред(Стр, Позиция));
			Позиция2 = Найти(Стр2, Фрагмент2);
			СуммаПозиций2 = Позиция2;
			Пока Позиция2 > 0 Цикл
				Если БезНаложения Тогда
					Если Найти(Сред(Стр2, 1, СуммаПозиций2 - 1), Фрагмент2) = 0 Тогда
						М.Добавить("" + ?(ИсключитьФрагменты, Сред(Стр2, 1, СуммаПозиций2 - 1), Сред(Стр2, 1, СуммаПозиций2 - 1 + СтрДлина(Фрагмент2))));
					КонецЕсли;
				Иначе
					М.Добавить("" + ?(ИсключитьФрагменты, Сред(Стр2, 1, СуммаПозиций2 - 1), Сред(Стр2, 1, СуммаПозиций2 - 1 + СтрДлина(Фрагмент2))));
				КонецЕсли;
				Позиция2 = Найти(Сред(Стр2, СуммаПозиций2 + 1), Фрагмент2);
				СуммаПозиций2 = СуммаПозиций2 + Позиция2;
			КонецЦикла;
			Стр = Сред(Стр, Позиция + 1);
			Позиция = Найти(Стр, Фрагмент1);
		КонецЦикла;
	КонецЕсли;
	
	Возврат М;
КонецФункции//СтрНайтиМежду

Процедура Тестирование()
	УдалитьФайлы("C:\444\ТестированиеТестовыхКодов", "*.os");
	
	ВыбранныеФайлы = НайтиФайлы("C:\444", "*.html", Истина);
	Для А = 0 По ВыбранныеФайлы.ВГраница() Цикл
		Если (ВыбранныеФайлы[А].ПолноеИмя = "C:\444\OneScriptFormsru\SaveFileDialog.Reset.html") Тогда
			Продолжить;
		КонецЕсли;
		ТекстДок = Новый ТекстовыйДокумент;
		ТекстДок.Прочитать(ВыбранныеФайлы[А].ПолноеИмя);
		Стр = ТекстДок.ПолучитьТекст();
		М = СтрНайтиМежду(Стр, "<details><summary>Тестовый код</summary>", "</PRE>", Ложь, );
		Если М.Количество() > 0 Тогда
			Для А2 = 0 По М.Количество() - 1 Цикл
				ТестовыйКод0 = СтрНайтиМежду(М[А2], "<DIV id=""cont", "</DIV>", Ложь, )[0];
				ТестовыйКод = СтрНайтиМежду(ТестовыйКод0, """>", "</DIV>", , )[0];
				Если Не (СокрЛП(ТестовыйКод) = "") Тогда
					СтрИмя = СтрЗаменить(ВыбранныеФайлы[А].Имя, "OneScriptForms.", "");
					СтрИмя = СтрЗаменить(СтрИмя, ".html", "");
					
					ТекстДокХХХ = Новый ТекстовыйДокумент;
					ТекстДокХХХ.УстановитьТекст(ТестовыйКод);
					Если М.Количество() > 1 Тогда
						ТекстДокХХХ.Записать("C:\444\ТестированиеТестовыхКодов\" + СтрИмя + А2 + ".os");
					Иначе
						ТекстДокХХХ.Записать("C:\444\ТестированиеТестовыхКодов\" + СтрИмя + ".os");
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры//Тестирование();


Функция Команда(ИмяФайла, Аргументы, Объект)
	ИнформацияЗапускаПроцесса1 = Ф.ИнформацияЗапускаПроцесса();
	ИнформацияЗапускаПроцесса1.ИмяФайла = ИмяФайла;
	ИнформацияЗапускаПроцесса1.Аргументы = Аргументы;
	ИнформацияЗапускаПроцесса1.СоздатьБезОкна = Истина;
	ИнформацияЗапускаПроцесса1.ИспользоватьОболочку = Ложь;
	ИнформацияЗапускаПроцесса1.СтильОкна = Ф.СтильОкнаПроцесса.Скрытое;
	ИнформацияЗапускаПроцесса1.ПеренаправитьСтандартныйВывод = Истина;

	Процесс1 = Ф.Процесс();
	Процесс1.НачальнаяИнформация = ИнформацияЗапускаПроцесса1;
	Процесс1.Начать();
	Если Не Процесс1.Завершен Тогда
		Стр1 = Процесс1.СтандартныйВывод.ПрочитатьСтроку();
		Пока Стр1 <> Неопределено Цикл
			Стр3 = "" + Стр1;
			Двоеточие = Прав(Стр3, 3);
			Двоеточие = Лев(Двоеточие, 1);
			Если (СтрНайти(Стр3, "!!!") > 0) или (СтрНайти(Стр3, "{") > 0)  или (Двоеточие <> ":") Тогда
				КолОшибок = КолОшибок + 1;
				СписокОшибок.Добавить(Стр3);
			КонецЕсли;
			Объект.ДобавитьТекст(Стр3 + Символы.ПС);
			Стр1 = Процесс1.СтандартныйВывод.ПрочитатьСтроку();
		КонецЦикла;
	Иначе
		Объект.ДобавитьТекст("Процесс1 завершен");
	КонецЕсли;
КонецФункции

Таймер = ТекущаяУниверсальнаяДатаВМиллисекундах();

ПодключитьВнешнююКомпоненту("C:\444\111\OneScriptForms\OneScriptForms\bin\Debug\OneScriptForms.dll");
Ф = Новый ФормыДляОдноСкрипта();
Форма1 = Ф.Форма();
Форма1.Ширина = 1100;
Форма1.Высота = 600;
Форма1.Отображать = Истина;
Форма1.Показать();

// Нужно добавить создание и очистку файла C:\444\FileTest.txt

КолТестов = 0;
КолОшибок = 0;
СписокОшибок = Новый СписокЗначений;

Тестирование();

ПолеВвода1 = Форма1.ЭлементыУправления.Добавить(Ф.ПолеВвода());
ПолеВвода1.МногострочныйРежим = Истина;
ПолеВвода1.Стыковка = Ф.СтильСтыковки.Заполнение;
ПолеВвода1.ЦветФона = Ф.Цвет().Черный;
ПолеВвода1.ОсновнойЦвет = Ф.Цвет().БледноЗеленый;
ПолеВвода1.ПолосыПрокрутки = Ф.ПолосыПрокрутки.Обе;
ПолеВвода1.ПринятиеВозврат = Истина;
ПолеВвода1.Перенос = Ложь;
ПолеВвода1.РегистрСимволов = Ф.РегистрСимволов.Стандартный;

ВыбранныеФайлы = НайтиФайлы("C:\444\ТестированиеТестовыхКодов", "*.os", Истина);

Для А = 0 По ВыбранныеФайлы.ВГраница() Цикл
	КолТестов = КолТестов + 1;
	Сообщить(" (" + Ф.Математика().Окр(((ТекущаяУниверсальнаяДатаВМиллисекундах()-Таймер)/1000)/60, 2) + " мин." + " " + КолТестов + " из " + ВыбранныеФайлы.Количество() + ")" + 
			"""C:\Program Files (x86)\OneScript\bin\oscript.exe"" " + ВыбранныеФайлы[А].ПолноеИмя);
	Команда("""C:\Program Files (x86)\OneScript\bin\oscript.exe""", ВыбранныеФайлы[А].ПолноеИмя, ПолеВвода1);
	Форма1.Фокус();
КонецЦикла;

Сообщить("КолТестов = " + КолТестов);

Если СписокОшибок.Количество() > 0 Тогда
	Сообщить("Ошибок = " + СписокОшибок.Количество());
	Сообщить("==================================");
	Для А = 0 По СписокОшибок.Количество() - 1 Цикл
		Сообщить("" + СписокОшибок.Получить(А).Значение);
	КонецЦикла;
	Сообщить("==================================");
Иначе
	Сообщить("Ошибок нет");
КонецЕсли;
Сообщить("Выполнено за: " + ((ТекущаяУниверсальнаяДатаВМиллисекундах()-Таймер)/1000)/60 + " мин." + " " + ТекущаяДата());

Форма1.Текст = "Общий Тест Тестовых Кодов";

Пока Ф.Продолжать Цикл
	Выполнить(Ф.ПолучитьСобытие());
КонецЦикла;